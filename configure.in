dnl Process this file with autoconf to produce a configure script. 
AC_REVISION($Id$)
# -*- shell-script -*-

# Copyright (C) 1999, 2000, 2001 by Martin Pool <mbp@samba.org>

AC_INIT(trace.c)

my_version=0.9.1dev3
my_package=libhsync
my_url=http://linuxcare.com.au/rproxy/
echo "Configuring $my_package $my_version   ($my_url)"
AM_INIT_AUTOMAKE($my_package, $my_version)

AM_DISABLE_SHARED
AM_PROG_LIBTOOL

AM_CONFIG_HEADER(config.h)

# Set up to get as many features as we can manage
AC_DEFINE(_FILE_OFFSET_BITS, 64)
AC_DEFINE(_GNU_SOURCE)

AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC
AC_ISC_POSIX
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_TYPE_SIZE_T
AC_TYPE_OFF_T

AC_C_CONST

# See if the compiler has the nice GNU feature of 
# putting argv[0] into a global
AC_MSG_CHECKING([for program_invocation_short_name])
AC_TRY_COMPILE([#include <errno.h>], 
    [strlen(program_invocation_short_name)],
    [ AC_DEFINE(HAVE_PROGRAM_INVOCATION_NAME)
      AC_MSG_RESULT(yes) ],
    [ AC_MSG_RESULT(no) ])

# On us4 SGI IRIX64, the linker reports
# ld32: WARNING 84 : /usr/lib32/libnsl.so is not used for resolving any symbol.

# HEY!  Do we need SGI cc to do 64-bit mode?  Does gcc only do 32bit?

# supply a snprintf if the system doesn't have one
AC_REPLACE_FUNCS(snprintf)
AC_CHECK_FUNCS(vsnprintf)
AC_CHECK_FUNCS(strerror mtrace)
AC_CHECK_FUNC(setreuid, [], [
    AC_CHECK_LIB(ucb, setreuid, [if echo $LIBS | grep -- -lucb >/dev/null ;then :; else LIBS="$LIBS -lc -lucb" USEUCB=y;fi])
])

AC_ARG_WITH(included-popt,
        [  --with-included-popt    use bundled popt library, not from system])

dnl Check for declarations
AC_MSG_CHECKING(for sockaddr.sin_len)
AC_CHECK_HEADERS(config.h unistd.h alloca.h libintl.h mcheck.h)
AC_EGREP_HEADER(sin_len, sys/socket.h, 
	[ AC_DEFINE(HAVE_SOCK_SIN_LEN)
          AC_MSG_RESULT(yes) ],
        [ AC_MSG_RESULT(no) ])

dnl Checks for libraries
if test x"$with_included_popt" != x"yes"
then
    AC_CHECK_LIB(popt, poptGetContext, , [with_included_popt=yes])
fi

AC_MSG_CHECKING("whether to use included libpopt")
if test x"$with_included_popt" = x"yes"
then
    AC_MSG_RESULT("$srcdir/popt")
    BUILD_POPT='popt/libpopt.a'
    CFLAGS="$CFLAGS -I $srcdir/popt"
else
    AC_MSG_RESULT(no)
fi

dnl Check configuration options
if test "$GCC" = "yes" 
then 
    # I'm fussy.
    CFLAGS="-Wall -Wshadow -Wundef -Wwrite-strings -Wmissing-prototypes -Wmissing-declarations -Wstrict-prototypes -Wpointer-arith -Wcast-qual -Wcast-align $CFLAGS"
fi
# TODO: Similar conditions for other known compilers.  For SUNWspro, use `-v'.

AC_ARG_ENABLE(ccmalloc,
	[  --enable-ccmalloc       use ccmalloc debugger (default no)],
	[ LIBS="$LIBS -lccmalloc -ldl" ])

AC_ARG_ENABLE(trace,
        [  --disable-trace         turn off library tracing])
AC_MSG_CHECKING("whether to include trace code")
if test "$enable_trace" = "yes" -o -z "$enable_trace"
then
    AC_MSG_RESULT(yes)
    AC_DEFINE(DO_HS_TRACE)
elif test "$enable_trace" = "no"
then
    AC_MSG_RESULT(no)
else
    AC_MSG_RESULT(failed)
    AC_MSG_ERROR("--enable-trace must be set to yes or no")
fi

AC_CHECK_SIZEOF([unsigned char], 1)
if test "$ac_cv_sizeof_unsigned_char" -ne 1
then
    AC_MSG_WARN(unsigned char seems to be $ac_cv_sizeof_unsigned_char bytes.  Expect trouble.)
fi

AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)
AC_CHECK_SIZEOF(off_t)

dnl Checks for header files.
AC_HEADER_STDC

# GNU library versioning: This is NOT the libhsync release number.
# Rather, it is a CURRENT:REVISION:AGE tuple describing binary
# compatibility to libtool.  It is only updated on major public
# releases where there is the possibility of binaries hanging around.

libhsync_libversion=2:0:0
AC_DEFINE_UNQUOTED(HS_LIBVERSION, "$libhsync_libversion")

AC_DEFINE_UNQUOTED(HS_CANONICAL_HOST, "$host")

AC_SUBST(libhsync_libversion)
AC_SUBST(OBJ_SAVE)
AC_SUBST(OBJ_RESTORE)
AC_SUBST(CC_SHOBJ_FLAG)
AC_SUBST(CCMALLOC_LIB)
AC_SUBST(net_libs)
AC_SUBST(LIBOBJS)
AC_SUBST(LIBTOOL_DEPS)
AC_SUBST(BUILD_POPT)

AC_OUTPUT([Makefile popt/Makefile man/Makefile])
